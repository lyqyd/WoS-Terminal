
;terminal functions version 1.3

	PUSH depndencycheck
	SET dependencycheck, 0
	CALL @checkversion array, 1.8
	ADD dependencycheck, #<arg0>
	CALL @checkversion string, 1.2
	ADD dependencycheck, #<arg0>
	COMPARE #<dependencycheck>, 2
	POP dependencycheck
	IF< @terminalafter
	ADD functionlist, 1
	SET functionlist-name-#<functionlist>, "terminal"
	SET functionlist-version-#<functionlist>, "1.3"
	GOTO @terminalafter

;@terminalregisterplugin pluginname, array pluginoptions, array plugincallbacks; RETURN void
@terminalregisterplugin
	PUSH index
	PUSH string
	SET terminalregplugoptarray, #<arg1>
	SET terminalregplugdcbarray, #<arg2>
	CALL @arrayadd terminalplugins, #<arg0>
	SET index, 0
	COMPARE *#<terminalregplugoptarray>, 0
	IF= @terminalregisterplugincallbacks
@terminalregisterpluginoptionsloop
		ADD index, 1
		PUSH #<terminalregplugoptarray>-#<index>
		POP string
		CALL @separateCommas #<string>
		ADD terminalpluginoptions, 1
		SET terminalpluginoptions-#<terminalpluginoptions>, "#<arg1>"
		SET terminalpluginoptions-level-#<terminalpluginoptions>, "#<arg0>"
		SET terminalpluginoptions-label-#<terminalpluginoptions>, "#<arg2>"		
		COMPARE #<index>, *#<terminalregplugoptarray>
		IF< @terminalregisterpluginoptionsloop
	SET index, 0
@terminalregisterplugincallbacks
	COMPARE *#<terminalregplugdcbarray>, 0
	IF= @terminalregisterplugindone
@terminalregisterplugincallbacksloop
		ADD index, 1
		PUSH #<terminalregplugdcbarray>-#<index>
		POP string
		CALL @separateCommas #<string>
		ADD terminalplugincallbacks, 1
		SET terminalplugincallbacks-#<terminalplugincallbacks>, #<arg1>
		SET terminalplugincallbacks-level-#<terminalplugincallbacks>, #<arg0>
		COMPARE #<index>, *#<terminalregplugdcbarray>
		IF< @terminalregisterplugincallbacksloop
@terminalregisterplugindone
	POP string
	POP index
	RETURN

;@terminalunregisterplugin pluginname, arraypluginoptions, arrayplugincallbacks; RETURN void
@terminalunregisterplugin
	PUSH index
	PUSH string
	SET terminalregplugoptarray, #<arg1>
	SET terminalregplugdcbarray, #<arg2>
	CALL @arrayfindvalue terminalplugins, #<arg0>
	CALL @arrayrem #<arg0>, #<arg1>
	SET index, 0
	COMPARE *#<terminalregplugoptarray>, 0
	IF= @terminalunregisterplugincallbacks
@terminalunregisterpluginoptionsloop
		ADD index, 1
		PUSH #<terminalregplugoptarray>-#<index>
		POP string
		CALL @separateCommas #<string>
		CALL @arrayfindvalue terminalpluginoptions, #<arg1>
		COMPARE #<arg1>, 0
		IF= @terminalunregisterpluginoptionsloopskip
		PUSH index
		SET index, #<arg1>
		CALL @arrayrem terminalpluginoptions, #<index>
		CALL @arrayrem terminalpluginoptions-level, #<index>
		CALL @arrayrem terminalpluginoptions-label, #<index>
		POP index
	@terminalunregisterpluginoptionsloopskip
		COMPARE #<index>, *#<terminalregplugoptarray>
		IF< @terminalunregisterpluginoptionsloop
	SET index, 0
@terminalunregisterplugincallbacks
	COMPARE *#<terminalregplugdcbarray>, 0
	IF= @terminalunregisterplugindone
@terminalunregisterplugincallbacksloop
		ADD index, 1
		PUSH #<terminalregplugdcbarray>-#<index>
		POP string
		CALL @separateCommas #<string>
		CALL @arrayfindvalue terminalplugincallbacks, #<arg1>
		COMPARE #<arg1>, 0
		IF= @terminalunregisterplugincallbacksloopskip
		PUSH index
		SET index, #<arg1>
		CALL @arrayrem terminalplugincallbacks, #<index>
		CALL @arrayrem terminalplugincallbacks-level, #<index>
		POP index
	@terminalunregisterplugincallbacksloopskip
		COMPARE #<index>, *#<terminalregplugdcbarray>
		IF< @terminalunregisterplugincallbacksloop
@terminalunregisterplugindone
	POP string
	POP index
	RETURN

;@terminalcheckpluginoptions int privelegelevel; RETURN str linelabel
@terminalcheckpluginoptions
	PUSH index
	PUSH value
	PUSH valuelen
	PUSH check
	SET index, 0
@terminalcheckpluginoptionsloop
		ADD index, 1
		PUSH terminalpluginoptions-#<index>
		POP value
		SET_LEN valuelen, #<value>
		SET_SUBSTR check, 0, #<valuelen>, #<terminalcomstr>
		STRCMP #<value>, #<check>
		IF= @terminalcheckpluginoptionsfound
		COMPARE #<index>, #<terminalpluginoptions>
		IF< @terminalcheckpluginoptionsloop
@terminalcheckpluginoptionsnomatch
	POP check
	POP valuelen
	POP value
	POP index
	RETURN 0
@terminalcheckpluginoptionsfound
	PUSH terminalpluginoptions-level-#<index>
	POP value
	COMPARE #<value>, #<arg0>
	IF> @terminalcheckpluginoptionsnomatch
	PUSH terminalpluginoptions-label-#<index>
	POP arg0
	POP check
	POP valuelen
	POP value
	POP index
	RETURN #<arg0>

;@terminalcheckplugincallbacks int privelegelevel; RETURN 0
@terminalgetplugincallbacks
	PUSH index
	PUSH value
	PUSH privelege
	SET index, 0
	SET privelege, "#<arg0>"
	SET terminalcallbacklist, 0
@terminalgetplugincallbacksloop
		ADD index, 1
		PUSH terminalplugincallbacks-level-#<index>
		POP value
		COMPARE #<value>, #<privelege>
		IF<= @terminalgetplugincallbacksadd
		COMPARE #<index>, #<terminalplugincallbacks>
		IF< @terminalgetplugincallbacksloop
@terminalgetplugincallbacksdone
	POP privelege
	POP value
	POP index
	RETURN 0
@terminalgetplugincallbacksadd
	PUSH terminalplugincallbacks-#<index>
	POP value
	CALL @arrayadd terminalcallbacklist, #<value>
	COMPARE #<index>, #<terminalplugincallbacks>
	IF< @terminalgetplugincallbacksloop
	GOTO @terminalgetplugincallbacksdone

;@terminalregisterhook hooklinelabel; RETURN bool registerfail
@terminalregisterhook
	STRCMP #<terminalhook>, ""
	IF= @terminalregisterhookgo
	STRCMP #<terminalhook>, #<arg0>
	IF= @terminalregisterhookgo
	RETURN 1
@terminalregisterhookgo
	SET terminalhook, #<arg0>
	RETURN 0

;@terminalunregisterhook hooklinelabel; RETURN bool unregisterfail
@terminalunregisterhook
	STRCMP #<terminalhook>, #<arg0>
	IF= @terminalunregisterhookgo
	RETURN 1
@terminalunregisterhookgo
	SET terminalhook, ""
	RETURN 0

;@terminalfind type, attribute, value; RETURN matchIDarray, attributeArray
@terminalfind
	PUSH typeid
	PUSH attribute
	PUSH findmax
	PUSH shortname
	PUSH subtype
	PUSH subcheck
	PUSH spell
	PUSH monster
	PUSH item
	SET spell, 767
	SET monster, 4095
	SET item, 5119
	SET subtype, -1
	STRSTR #<arg0>, ":"
	IF= @terminalfindsubtypedone
	NTH_TOKEN subtype, 1, #<arg0>, ":"
	NTH_TOKEN arg0, 0, #<arg0>, ":"
@terminalfindsubtypedone
	SET findmax, *#<arg0>
	SET_SUBSTR shortname, 0, 1, #<arg0>
	SET typeid, "#<arg0>.id"
	SET attribute, "#<arg0>.#<arg1>"
	SET #<typeid>, 0
	SET terminalfindresults, 0
	SET terminalfindattributes, 0
	COMPARE #<subtype>, 0
	IF>= @terminalfindsubtype
	STRSTR "-name-desc-alignment-skin-class-element-" -#<arg1>-
	IF> @terminalfindexactloop
@terminalfindloop
		COMPARE *#<typeid>, #<findmax>
		IF>= @terminalfindloopdone
		ADD #<typeid>, 1
		COMPARE *#<attribute>, #<arg2>
		IF< @terminalfindloop
		ADD terminalfindresults, 1
		ADD terminalfindattributes, 1
		SET terminalfindresults-#<terminalfindresults>, #<shortname>*#<typeid>
		SET terminalfindattributes-#<terminalfindattributes>, *#<attribute>
		GOTO @terminalfindloop
@terminalfindsubtype
	SET spell, "element"
	SET monster, "element"
	SET item, "class"
	SET subcheck, #<arg0>.*<arg0>
	STRSTR "-name-desc-alignment-skin-class-element-" -#<arg1>-
	IF> @terminalfindexactloopsubtype
@terminalfindloopsubytpe
		COMPARE *#<typeid>, #<findmax>
		IF>= @terminalfindloopdone
		ADD #<typeid>, 1
		COMPARE *#<subcheck>, #<subtype>
		IF!= @terminalfindloopsubtype
		COMPARE *#<attribute>, #<arg2>
		IF< @terminalfindloopsubtype
		ADD terminalfindresults, 1
		ADD terminalfindattributes, 1
		SET terminalfindresults-#<terminalfindresults>, #<shortname>*#<typeid>
		SET terminalfindattributes-#<terminalfindattributes>, *#<attribute>
		GOTO @terminalfindloopsubtype
@terminalfindexactloop
		COMPARE *#<typeid>, #<findmax>
		IF>= @terminalfindloopdone
		ADD #<typeid>, 1
		STRCMP *#<attribute>, #<arg2>
		IF!= @terminalfindexactloop
		ADD terminalfindresults, 1
		SET terminalfindresults-#<terminalfindresults>, #<shortname>*#<typeid>
		GOTO @terminalfindexactloop
@terminalfindexactloopsubtype
		COMPARE *#<typeid>, #<findmax>
		IF>= @terminalfindloopdone
		ADD #<typeid>, 1
		COMPARE *#<subcheck>, #<subtype>
		IF!= @terminalfindexactloopsubtype
		STRCMP *#<attribute>, #<arg2>
		IF!= @terminalfindexactloopsubtype
		ADD terminalfindresults, 1
		SET terminalfindresults-#<terminalfindresults>, #<shortname>*#<typeid>
		GOTO @terminalfindexactloopsubtype
@terminalfindloopdone
	POP item
	POP monster
	POP spell
	POP subcheck
	POP subtype
	POP shortname
	POP findmax
	POP attribute
	POP typeid
	RETURN terminalfindresults, terminalfindattributes

;end of terminalfunctions
@terminalafter

